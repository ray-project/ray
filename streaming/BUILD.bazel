# Bazel build
# C/C++ documentation: https://docs.bazel.build/versions/master/be/c-cpp.html

load("@com_github_grpc_grpc//bazel:cython_library.bzl", "pyx_library")
load("@rules_proto_grpc//python:defs.bzl", "python_proto_compile")

proto_library(
    name = "streaming_proto",
    srcs = ["src/protobuf/streaming.proto"],
    visibility = ["//visibility:public"],
)

cc_proto_library(
    name = "streaming_cc_proto",
    deps = [":streaming_proto"],
)

proto_library(
    name = "streaming_queue_proto",
    srcs = ["src/protobuf/streaming_queue.proto"],
)

cc_proto_library(
    name = "streaming_queue_cc_proto",
    deps = ["streaming_queue_proto"],
)

# ray shared lib
cc_binary(
    name = "ray_util.so",
    visibility = ["//visibility:public"],
    linkshared=1,
    deps = ["//:ray_util"],
)

cc_binary(
    name = "ray_common.so",
    visibility = ["//visibility:public"],
    linkshared=1,
    deps = ["//:ray_common"],
)

cc_binary(
    name = "core_worker_lib.so",
    linkshared=1,
    deps = ["//:core_worker_lib"],
)


cc_library(
    name = "streaming_util",
    srcs = glob([
        "src/util/*.cc",
    ]),
    hdrs = glob([
        "src/util/*.h",
    ]),
    includes = [
        "src",
    ],
    visibility = ["//visibility:public"],
    deps = [
        "ray_util.so",
        "@boost//:any",
        "@com_google_googletest//:gtest",
    ],
)

cc_library(
    name = "streaming_config",
    srcs = glob([
        "src/config/*.cc",
    ]),
    hdrs = glob([
        "src/config/*.h",
    ]),
    deps = [
        ":streaming_cc_proto",
        ":streaming_util",
        "ray_common.so",
    ],
)

cc_library(
    name = "streaming_message",
    srcs = glob([
        "src/message/*.cc",
    ]),
    hdrs = glob([
        "src/message/*.h",
    ]),
    deps = [
        ":streaming_config",
        ":streaming_util",
        "ray_common.so",
    ],
)

cc_library(
    name = "streaming_queue",
    srcs = glob([
        "src/queue/*.cc",
    ]),
    hdrs = glob([
        "src/queue/*.h",
    ]),
    deps = [
        ":streaming_config",
        ":streaming_message",
        ":streaming_queue_cc_proto",
        ":streaming_util",
        "core_worker_lib.so",
        "ray_common.so",
        "ray_util.so",
        "@boost//:asio",
        "@boost//:thread",
    ],
)

cc_library(
    name = "streaming_lib",
    srcs = glob([
        "src/*.cc",
    ]),
    hdrs = glob([
        "src/*.h",
        "src/queue/*.h",
        "src/test/*.h",
    ]),
    includes = ["src"],
    visibility = ["//visibility:public"],
    deps = [
        ":streaming_config",
        ":streaming_message",
        ":streaming_queue",
        ":streaming_util",
        "ray_common.so",
        "ray_util.so",
        "@boost//:circular_buffer",
    ],
)

test_common_deps = [
    ":streaming_lib",
    "//:ray_common",
    "//:ray_util",
    "//:core_worker_lib",
]

cc_binary(
    name = "streaming_test_worker",
    srcs = [
        "src/test/mock_actor.cc",
    ],
    copts = ["-Isrc/test/"],
    includes = [
        "streaming/src/test",
    ],
    deps = test_common_deps
)

cc_binary(
    name = "streaming_message_ring_buffer_tests",
    srcs = [
        "src/test/ring_buffer_tests.cc",
    ],
    includes = [
        "streaming/src/test",
    ],
    deps = test_common_deps
)

cc_binary(
    name = "streaming_message_serialization_tests",
    srcs = [
        "src/test/message_serialization_tests.cc",
    ],
    deps = test_common_deps
)

cc_binary(
    name = "streaming_mock_transfer",
    srcs = [
        "src/test/mock_transfer_tests.cc",
    ],
    deps = test_common_deps
)

cc_binary(
    name = "streamingqueue_tests",
    srcs = [
        "src/test/queue_tests_base.h",
        "src/test/streamingqueue_tests.cc",
    ],
    copts = ["-Isrc/test/"],
    deps = test_common_deps
)

cc_binary(
    name = "streaming_util_tests",
    srcs = [
        "src/test/streaming_util_tests.cc",
    ],
    deps = test_common_deps
)

genrule(
    name = "run_streaming_common_tests",
    srcs = [
        ":streaming_message_ring_buffer_tests",
        ":streaming_message_serialization_tests",
        ":streaming_util_tests",
        ":streaming_mock_transfer",
    ],
    outs = ["streaming_test_log.out"],
    cmd = """
      set -x &&
      date > $@
      $(location streaming_message_ring_buffer_tests) >> $@ &&
      $(location streaming_message_serialization_tests) >> $@ &&
      $(location streaming_util_tests) >> $@ &&
      $(location streaming_mock_transfer) >> $@
    """,
)

python_proto_compile(
    name = "streaming_py_proto",
    deps = ["//streaming:streaming_proto"],
)

genrule(
    name = "cp_streaming_py_proto_generated",
    srcs = [
        ":streaming_py_proto",
    ],
    outs = [
        "cp_streaming_py_proto_generated.out",
    ],
    cmd = """
        set -e
        set -x
        WORK_DIR=$$(pwd)
        # Copy generated files.
        GENERATED_DIR=$$WORK_DIR/streaming/python/generated
        rm -rf $$GENERATED_DIR
        mkdir -p $$GENERATED_DIR
        for f in $(locations //streaming:streaming_py_proto); do
            cp $$f $$GENERATED_DIR
        done
        echo $$(date) > $@
    """,
    local = 1,
    visibility = ["//visibility:public"],
)
