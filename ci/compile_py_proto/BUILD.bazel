load("@rules_python//python:pip.bzl", "compile_pip_requirements")
load("@rules_python//python:defs.bzl", "py_binary")
load("@py_deps_compile_py_proto//:requirements.bzl", "requirement")

compile_pip_requirements(
    name = "requirements_compile_py_proto",
    requirements_in = "requirements_compile_py_proto.in",
    requirements_txt = "requirements_compile_py_proto.txt",
    extra_args = ["--allow-unsafe"],
    tags = ["team:ci"],
    visibility = ["//visibility:public"],
)

py_binary(
    name = "grpc_tools_protoc",
    srcs = ["grpc_tools_protoc.py"],
    deps = [
        requirement("grpcio-tools"),
    ],
    exec_compatible_with = ["//:hermetic_python"],
    tags = ["team:ci"],
    visibility = ["//visibility:public"],
)

genrule(
    name = "compile_py_proto",
    srcs = [
        "@io_opencensus_proto//opencensus/proto/metrics/v1:metrics.proto",
        "@io_opencensus_proto//opencensus/proto/resource/v1:resource.proto",
        "//src/ray/protobuf:core_protos",
        "//src/ray/protobuf:serve_protos",
    ],
    cmd = """
        TMP=$$(mktemp -d)

        mkdir -p $${TMP}/core
        $(location :grpc_tools_protoc) --python_out=$${TMP}/core --grpc_python_out=$${TMP}/core $(locations //src/ray/protobuf:core_protos) --proto_path=. --proto_path=$$(dirname $(locations @io_opencensus_proto//opencensus/proto/metrics/v1:metrics.proto))/../../../..
        mkdir -p opencensus/proto/metrics/v1
        mkdir -p opencensus/proto/resource/v1
        cp $(locations @io_opencensus_proto//opencensus/proto/metrics/v1:metrics.proto) opencensus/proto/metrics/v1/metrics.proto
        cp $(locations @io_opencensus_proto//opencensus/proto/resource/v1:resource.proto) opencensus/proto/resource/v1/resource.proto
        $(location :grpc_tools_protoc) --python_out=$${TMP}/core --grpc_python_out=$${TMP}/core opencensus/proto/metrics/v1/metrics.proto opencensus/proto/resource/v1/resource.proto --proto_path=.
        mv $${TMP}/core/src/ray/protobuf/*.py $${TMP}/core/
        mv $${TMP}/core/src/ray/protobuf/**/*.py $${TMP}/core/
        mv $${TMP}/core/opencensus/proto/**/v1/*.py $${TMP}/core/

        mkdir -p $${TMP}/serve
        $(location :grpc_tools_protoc) --python_out=$${TMP}/serve --grpc_python_out=$${TMP}/serve $(locations //src/ray/protobuf:serve_protos) --proto_path=.
        mv $${TMP}/serve/src/ray/protobuf/*.py $${TMP}/serve/

        # NOTE(hchen): Protobuf doesn't allow specifying Python package name. So we use this `sed`
        # command to change the import path in the generated file.
        # shellcheck disable=SC2006
        files=(
            `ls $${TMP}/*/*_pb2*.py`
        )
        sed -i -E 's/from src.ray.protobuf/from ./' "$${files[@]}"
        # TODO(sang): Build our own proto instead of creating a new proto for opencensus separately.
        # https://github.com/ray-project/ray/issues/31358
        sed -i -E 's/from opencensus.proto.metrics.v1 import/from . import/' "$${files[@]}"
        sed -i -E 's/from opencensus.proto.resource.v1 import/from . import/' "$${files[@]}"
        # TODO(rickyx): Remove this experimental path rewrite once we move instance manager proto out
        # of experimental.
        autoscale_files=(`ls $${TMP}/core/instance_manager_pb2*.py`)
        sed -i -E 's/from ..experimental/from ./' "$${autoscale_files[@]}"

        # Help the generated serve files to have the correct module
        serve_files=(`ls $${TMP}/serve/*_pb2*.py`)
        sed -i -E 's/'"'"'src.ray.protobuf./'"'"'ray.serve.generated./' "$${serve_files[@]}"

        pushd $${TMP}/core
        tar -cvf compile_py_proto_core.tar *.py
        popd

        pushd $${TMP}/serve
        tar -cvf compile_py_proto_serve.tar *.py
        popd

        mv $${TMP}/core/compile_py_proto_core.tar $(location :compile_py_proto_core.tar)
        mv $${TMP}/serve/compile_py_proto_serve.tar $(location :compile_py_proto_serve.tar)
        rm -r $${TMP}
    """,
    tools = [
        ":grpc_tools_protoc",
    ],
    outs = [
        "compile_py_proto_core.tar",
        "compile_py_proto_serve.tar"
    ],
    visibility = ["//visibility:public"],
)

sh_binary(
    name = "install_py_proto",
    srcs = [
        ":install_py_proto.sh",
    ],
    data = [
        ":compile_py_proto_core.tar",
        ":compile_py_proto_serve.tar",
    ],
    args = [
        "$(location :compile_py_proto_core.tar)",
        "$(location :compile_py_proto_serve.tar)",
    ],
)
