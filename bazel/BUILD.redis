load("@rules_foreign_cc//foreign_cc:defs.bzl", "make")

exports_files(
    [
        "redis-server.exe",
        "redis-cli.exe",
    ],
    visibility = ["//visibility:public"],
)

filegroup(
    name = "all_srcs",
    srcs = glob(
        include = ["**"],
        exclude = ["*.bazel"],
    ),
)
    

make(
    name = "redis",
    args = [
        "BUILD_TLS=yes",
        "-s",
    ],
    copts = [
        "-DLUA_USE_MKSTEMP",
        "-Wno-pragmas",
        "-Wno-empty-body",
        "-fPIC",
    ],
    visibility = ["//visibility:public"],
    lib_source = ":all_srcs",
    deps = [
        "@openssl//:openssl",
    ],
    out_binaries = [
        "redis-server",
        "redis-cli"
    ]
)

genrule(
    name = "bin",
    srcs = [":redis"],
    outs = [
        "redis-server",
        "redis-cli",
    ],
    cmd = """
    mkdir tmp-redis-bin
    cp -rf $(locations :redis) ./tmp-redis-bin/
    cp tmp-redis-bin/redis-server $(location redis-server)
    chmod +x $(location redis-server)
    cp tmp-redis-bin/redis-cli $(location redis-cli)
    chmod +x $(location redis-cli)
    rm -rf tmp-redis-bin
    """,
    visibility = ["//visibility:public"],
    tags = ["local"],
)

# This is a build for mac without openssl.
# rules_foreign_cc somehow pick the wrong toolchain in CI.
genrule(
    name = "bin-mac",
    srcs = glob(["**"]),
    outs = [
        "redis-server-mac",
        "redis-cli-mac",
    ],
    cmd = """
        unset CC LDFLAGS CXX CXXFLAGS
        tmpdir="redis.tmp"
        p=$(location Makefile)
        cp -p -L -R -- "$${p%/*}" "$${tmpdir}"
        chmod +x "$${tmpdir}"/deps/jemalloc/configure
        parallel="$$(getconf _NPROCESSORS_ONLN || echo 1)"
        make -s -C "$${tmpdir}" -j"$${parallel}" V=0 CFLAGS="$${CFLAGS-} -DLUA_USE_MKSTEMP -Wno-pragmas -Wno-empty-body"
        mv "$${tmpdir}"/src/redis-server $(location redis-server-mac)
        chmod +x $(location redis-server-mac)
        mv "$${tmpdir}"/src/redis-cli $(location redis-cli-mac)
        chmod +x $(location redis-cli-mac)
        rm -r -f -- "$${tmpdir}"
    """,
    visibility = ["//visibility:public"],
    tags = ["local"],
)

