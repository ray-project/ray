exports_files(
    [
        "redis-server.exe",
        "redis-cli.exe",
    ],
    visibility = ["//visibility:public"],
)

genrule(
    name = "bin",
    srcs = glob(["**"]),
    outs = [
        "redis-server",
        "redis-cli",
    ],
    cmd = """
        tmpdir="redis.tmp"
        p=$(location Makefile)
        cp -p -L -R -- "$${p%/*}" "$${tmpdir}"
        chmod +x "$${tmpdir}"/deps/jemalloc/configure
        parallel="$$(getconf _NPROCESSORS_ONLN || echo 1)"
        make -s -C "$${tmpdir}" -j"$${parallel}" V=0 CFLAGS="$${CFLAGS-} -fPIC -DLUA_USE_MKSTEMP -Wno-pragmas -Wno-empty-body"
        mv "$${tmpdir}"/src/redis-server $(location redis-server)
        chmod +x $(location redis-server)
        mv "$${tmpdir}"/src/redis-cli $(location redis-cli)
        chmod +x $(location redis-cli)
        rm -r -f -- "$${tmpdir}"
    """,
    visibility = ["//visibility:public"],
)

# This library is for internal hiredis use, because hiredis assumes a
# different include prefix for itself than external libraries do.
cc_library(
    name = "_hiredis",
    hdrs = [
        "deps/hiredis/dict.c",
        "deps/hiredis/dict.h",
        "deps/hiredis/fmacros.h",
    ],
    strip_include_prefix = "deps/hiredis",
)

cc_library(
    name = "hiredis",
    srcs = glob(
        [
            "deps/hiredis/*.c",
            "deps/hiredis/*.h",
        ],
        exclude =
        [
            "deps/hiredis/test.c",
        ],
    ),
    hdrs = glob([
        "deps/hiredis/*.h",
        "deps/hiredis/adapters/*.h",
    ]),
    strip_include_prefix = "deps",
    deps = [
        ":_hiredis",
    ],
    visibility = ["//visibility:public"],
)
